## This code implements algorithms in
## Rudolfo Gorenflo, Joulia Loutchko and Yuri Loutchko,
## *Computation of the Mittag-Leffler function and its derivative*,  Fract. Calc. Appl. Anal, **(2002)**
"""
@ARTICLE{Gorenflo_computationof,
    author = {Rudolf Gorenflo and Joulia Loutchko and Yuri Luchko},
    title = {Computation of the Mittag-Leffler function EŒ±,Œ≤(z) and its derivative},
    journal = {FRACT. CALC. APPL. ANAL},
    year = {},
    pages = {2002}
}
"""
# This MittagLeffler function is modified from [John Lapeyre](https://github.com/jlapeyre)'s [MittagLeffler.jl](https://github.com/jlapeyre/MittagLeffler.jl)
# Since Mittag Leffler functions is widely used in Fractional Differential Equation, so we decided to has the Mittag Leffler function build in.
# Credits go to JohnüôÇ, bugs are mine.

import QuadGK: quadgk

function ourquadgk(f, a, b)
    (result, _) = quadgk(f, a, b; order=17)
    return result
end

function P(Œ±, Œ≤, œµ, œï, z)
    œâ = œï*(1 + (1 - Œ≤)/Œ±) + œµ^(1/Œ±)*sin(œï/Œ±)
    return 1/(2*Œ±*pi) * œµ^(1 + (1 - Œ≤)/Œ±)*exp(œµ^(1/Œ±) * cos(œï/Œ±)) * (cos(œâ) + im*sin(œâ))/(œµ*exp(im*œï)-z)
end

Pint(Œ±, Œ≤, œµ, z) = ourquadgk(œï -> P(Œ±, Œ≤, œµ, œï, z), -Œ±*pi, Œ±*pi)
Pint(Œ±, Œ≤, z) = Pint(Œ±, Œ≤, 1, z)

function Kint(Œ±, Œ≤, a, œá0, z)
    return ourquadgk(œá -> K(Œ±, Œ≤, œá, z), a, œá0)
end

function K(Œ±, Œ≤, r, z)
    den = (r^2 - 2*r*z*cos(Œ±*pi) + z^2)
    return 1/(Œ±*pi) * r^((1-Œ≤)/Œ±) * exp(-r^(1/Œ±))*( r*sin(pi*(1-Œ≤)) - z*sin(pi*(1 - Œ≤ + Œ±)))/den
end

Kint(Œ±, Œ≤, œá0, z) = Kint(Œ±, Œ≤, 0, œá0, z)

mpow(x::Complex, y) = x^y
mpow(x::Real, y) = x >= 0 ? x^y : Complex(x, 0)^y

# The following gymnastics are to get around the type-unstable mpow
function sum2(Œ±, Œ≤, z::Real, k0)
    return z > 0 ? sum2_pos(Œ±, Œ≤, z, k0) : sum2_neg(Œ±, Œ≤, z, k0)
end
sum2(Œ±, Œ≤, z::Complex, k0) = sum2_neg(Œ±, Œ≤, z, k0)

for funcname in (:sum2_neg, :sum2_pos)
    local zarg
    local stype
    if funcname == :sum2_neg
        zarg = :( (z + Complex(0,0) ))
        stype = :( typeof(z + Complex(0,0) ) )
    else
        zarg = :z
        stype = :( typeof(z) )
    end
    @eval function ($funcname)(Œ±, Œ≤, z, k0)
        s::($stype) = zero($stype)
        for k=1:k0
            arg = Œ≤ - Œ± * k
            if !( round(arg) == arg && arg < 0)
                s += ($zarg)^(-k) / gamma(arg)
            end
        end
        return s
    end
end

function choosesum(Œ±, Œ≤, z, œÅ)
    k0 = floor(Int, -log(œÅ)/log(abs(z)))
    if abs(angle(z)) < pi*Œ±/4 + 1//2 * min(pi, pi*Œ±)
        return 1/Œ± * z^((1-Œ≤)/Œ±) * exp(z^(1/Œ±)) - sum2(Œ±, Œ≤, z, k0)
    else
        return - sum2(Œ±, Œ≤, z, k0)
    end
end

function mittleffsum(Œ±, Œ≤, z)
    k0::Int = floor(Int, Œ±) + 1
    s = zero(z)
    for k=0:(k0 - 1)
        s += mittleff(Œ± / k0, Œ≤, mpow(z, (1 // k0)) * exp(2pi * im * k / k0))
    end
    return s / k0
end

# mittleffsum sometimes passes complex z with magnitude nearly equal to 1
# Then k0 is astronomically high. Because the gamma function is evaluated by a call to gsl,
# the loop is not interruptible.
# So, we compute k0 using only the real part of k0.
# The effect on the accuracy is not controlled.
function mittleffsum2(Œ±, Œ≤, z, œÅ)
    zr = real(z)
    k0 = max(ceil(Int, (1-Œ≤)/Œ±), ceil(Int, log(œÅ*(1-abs(zr)))/log(abs(zr))))
    s = zero(z)
    for k=0:k0
        s += z^k/gamma(Œ≤+Œ±*k)
    end
    return s
end


function mittleffints(Œ±, Œ≤, z, œÅ)
    az = abs(z)
    ab = abs(Œ≤)
    œá0 = Œ≤ >= 0 ?
      max(1, 2*az, (-log(pi*œÅ/6))^Œ±) :
      max((ab+1)^Œ±, 2*az, (-2*log( pi*œÅ/(6*(ab+2)*(2*ab)^ab)))^Œ±)
    aaz = abs(angle(z))
    if aaz > Œ± * pi
        if Œ≤ <= 1
            return Kint(Œ±, Œ≤, œá0, z)
        else
            return Kint(Œ±, Œ≤, 1, œá0, z) + Pint(Œ±, Œ≤, z)
        end
    elseif aaz < pi*Œ±
        if Œ≤ <= 1
            return Kint(Œ±, Œ≤, œá0, z) + (1/Œ±)*z^((1-Œ≤)/Œ±) * exp(z^(1/Œ±))
        else
            return Kint(Œ±, Œ≤, az/2, œá0, z) + Pint(Œ±, Œ≤, (az)/2, z) + (1/Œ±)*z^((1-Œ≤)/Œ±) * exp(z^(1/Œ±))
        end
    else
        return Kint(Œ±, Œ≤, (az+1)/2, œá0, z) + Pint(Œ±, Œ≤, (az+1)/2, z)
    end
end

"""
    mittlefferr(Œ±,z,œÅ)

Compute mittlefferr(Œ±,1,z,œÅ).
"""
mittlefferr(Œ±, z, œÅ) = mittlefferr(Œ±, 1, z, œÅ)


"""
    mittlefferr(Œ±,Œ≤,z,œÅ)

Compute the Mittag-Leffler function at `z` for parameters `Œ±,Œ≤` with
accuracy `œÅ`.
"""
function mittlefferr(Œ±, Œ≤, z, œÅ::Real)
    œÅ > 0 || throw(DomainError(œÅ))
    _mittlefferr(Œ±, Œ≤, z, œÅ)
end

_mittlefferr(Œ±::Real, Œ≤::Real, z::Real, œÅ::Real) = real(_mittleff(Œ±, Œ≤, z, œÅ))
_mittlefferr(Œ±::Real, Œ≤::Real, z::Complex, œÅ::Real) = _mittleff(Œ±, Œ≤, z, œÅ)

# The second definition would work for both complex and real
myeps(x) = x |> one |> float |> eps
myeps(x::Complex) =  x |> real |> myeps

"""
    mittleff(Œ±, Œ≤, z)

Compute the Mittag-Leffler function at `z` for parameters `Œ±, Œ≤`.
"""
mittleff(Œ±, Œ≤, z) = _mittleff(Œ±, Œ≤, float(z))
mittleff(Œ±, Œ≤, z::Union{Integer,Complex{T}}) where {T<:Integer} = mittleff(Œ±, Œ≤, float(z))

#mittleff(Œ±, Œ≤, z) = _mittlefferr(Œ±,Œ≤,z,myeps(z))

"""
    mittleff(Œ±, z)

Compute `mittleff(Œ±, 1, z)`.
"""
mittleff(Œ±, z) = _mittleff(Œ±,1,z)
#mittleff(Œ±, z::Union{Integer,Complex{T}}) where {T<:Integer} = mittleff(Œ±, float(z))

mittleff(Œ±, vec::Vector) = map(x -> mittleff(Œ±, 1, x), vec)
mittleff(Œ±, Œ≤, vec::Vector) = map(x -> mittleff(Œ±, Œ≤, x), vec)


function _mittleff_special_beta_one(Œ±,z)
    z == 0 && return one(z)
    if Œ± == 1/2 && abs(z) < 10
        return exp(z^2) * erfc(-z)
    end
    Œ± == 0 && return 1/(1-z)  # FIXME needs domain check
    Œ± == 1 && return exp(z)
    zc = isa(z, Real) && z < 0 ? complex(z) : z # Julia sometimes requires explicit complex numbers for efficiency
    Œ± == 2 && return cosh(sqrt(zc))
    Œ± == 3 && return (1//3) * (exp(zc^(1//3)) + 2 * exp(-zc^(1//3)/2) * cos(sqrt(convert(typeof(zc),3))/2 * zc^(1//3)))
    Œ± == 4 && return (1//2) * (cosh(zc^(1//4)) + cos(zc^(1//4)))
    return nothing
end

function _mittleff_slow_with_eps(Œ±, Œ≤, z, œÅ)
    az = abs(z)
    az < 1 && return mittleffsum2(Œ±, Œ≤, z, œÅ)
    az > floor(10 + 5*Œ±) && return choosesum(Œ±, Œ≤, z, œÅ)
    return mittleffints(Œ±, Œ≤, z, œÅ)
end

_mittleff(Œ±::Real, Œ≤::Real, z::Real) = real(_mittleff0(Œ±, Œ≤, z))
_mittleff(Œ±, Œ≤, z) = _mittleff0(Œ±, Œ≤, z)

function _mittleff0(Œ±, Œ≤, z)
    if Œ≤ == 1
        res = _mittleff_special_beta_one(Œ±,z)
        res != nothing && return res
    end
    z == 0 && return 1/gamma(Œ≤)
    if Œ≤ == 2
        Œ± == 1 && return (exp(z) - 1)/z
        zc = isreal(z) && z < 0 ? complex(z) : z
        Œ± == 2 && return sinh(sqrt(zc))/sqrt(zc)
    end
    1 < Œ± && return mittleffsum(Œ±,Œ≤,z)
    œÅ = myeps(z)
    return _mittleff_slow_with_eps(Œ±, Œ≤, z, œÅ)
end

function _mittleff(Œ±, Œ≤, z, œÅ)
    if Œ≤ == 1
        res = _mittleff_special_beta_one(Œ±, z)
        res != nothing && return res
    end
    z == 0 && return 1/gamma(Œ≤)
    Œ± == 1 && Œ≤ == 1 && return(exp(z))
    1 < Œ± && return mittleffsum(Œ±, Œ≤, z)
    return _mittleff_slow_with_eps(Œ±, Œ≤, z, œÅ)
end

"""
    mittleffderiv(Œ±,Œ≤,z)

Compute the derivative of the Mittag-Leffler function at `z` for parameters `Œ±, Œ≤`.
"""
function mittleffderiv(Œ±, Œ≤, z)
    # Derivative of Mittag Leffler function WRT to main argument Z.
    # Take q = 0.5. Paper requires |z| <= q < 1.
    q = 1//2
    #case 1, small z
    if abs(z) <= q
        œâ = Œ± + Œ≤ - 3//2
        D = Œ±^2 - 4*Œ±*Œ≤ + 6*Œ± + 1
        #k1
        if Œ±>1
            k‚ÇÅ = ((2 - Œ± - Œ≤)/(Œ± - 1)) + 1
        elseif Œ± > 0 && Œ± <= 1 && D <= 0
            k‚ÇÅ = ((3 - Œ± - Œ≤)/Œ±) + 1
        else
            k‚ÇÅ = maximum([((3-Œ±-Œ≤)/Œ±) + 1, ((1-2*œâ*Œ± + sqrt(D))/(2*(Œ±^2)))+1])
        end
        k‚ÇÄ = maximum([k‚ÇÅ, log(myeps(z)*(1-abs(z)))/log(abs(z))])
        k‚ÇÄ = ceil(Int, k‚ÇÄ) #take ceiling (not specified in paper whether floor or ceiling)
        out = zero(z)
        for k in 0:k‚ÇÄ
            out = out + ((k + 1)*z^k)/(gamma(Œ± + Œ≤ + Œ±*k))
        end
    #case 2, larger z
    else
        out = (mittleff(Œ±, Œ≤-1, z) - (Œ≤ - 1)*mittleff(Œ±, Œ≤, z))/(Œ±*z)
    end
    return out
end

"""
    mittleffderiv(Œ±,z)

Compute mittleffderiv(Œ±,1,z)
"""
mittleffderiv(Œ±, z) = mittleffderiv(Œ±, 1, z)